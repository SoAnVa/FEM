module GLOBALVAR
implicit double precision (A-H,O-Z)
	!geomet 
INTEGER :: NNODE, NELE, NSEC, NMAT,NDOF
INTEGER, DIMENSION(:), ALLOCATABLE, SAVE:: ISEC, IMAT
INTEGER, DIMENSION(:,:),ALLOCATABLE, SAVE:: IN, IDOF
REAL(4), DIMENSION(:,:), ALLOCATABLE, SAVE:: COORD, CSEC, CMAT, ELOADS, PRES
REAL(4), DIMENSION(:), ALLOCATABLE, SAVE::VLOADS
 !ASSEMBLY SUBROUTINE 
REAL(4), DIMENSION(:,:), ALLOCATABLE, SAVE::VK
REAL(4), DIMENSION(6,6)::ST
REAL(4), DIMENSION(6)::EQFG, QSL

!SOLVE
REAL(4), DIMENSION(:), ALLOCATABLE, SAVE::VDISP
	
END MODULE GLOBALVAR

PROGRAM FRAME 
USE GLOBALVAR
!OPENING OF INPUT/OUTPUT FILE
OPEN(11, FILE='INPUT.TXT', STATUS='UNKNOWN')
OPEN(12, FILE='OUTPUT.TXT', STATUS='UNKNOWN')
OPEN(9,FILE='MKK.TMP',STATUS='UNKNOWN')
! CALL THE SUBROUTINE 
CALL GEOMET 
CALL SCODE
CALL LOADS

CALL ASSEMB

CALL JOINTS
CALL SOLVE
CALL STRESS
!FILE CLOSE 
CLOSE(11)
CLOSE(12)
CLOSE(9)
END PROGRAM FRAME 

SUBROUTINE GEOMET 
USE GLOBALVAR
INTEGER :: IDNODE, IDSEC, IDMAT, IDELE, I, J, IDCODE
CHARACTER(LEN=80):: REC, TITLE 
!READING/WRINTING THE NODES 
READ(11,*) TITLE 
READ(11,*) REC 
READ(11,*) NNODE 
IF (NNODE<2) THEN 
	WRITE (12,'(/,1X,"DATA ERROR NNODE")')
	STOP 
	RETURN 
END IF 
WRITE (12,'(1X,"NUMBER OF NODES",1X,I3)') NNODE 
WRITE(12,101) 'NODE', 'COORDINATEX', 'COORDINATEY'
ALLOCATE(COORD(NNODE,2)) 
DO I=1,NNODE 
	READ(11,*) IDNODE, COORD(IDNODE,:)
	WRITE(12,102) IDNODE, COORD(IDNODE,:)
END DO 
! READING WRTING CROSS SECTION 
READ(11,*) REC 
READ(11,*) NSEC 
IF (NSEC<1) THEN 
	WRITE (12,'(/,1X,"DATA ERROR NSEC")')
	STOP 
	RETURN 
END IF
WRITE(12,'(/,1X,"NUMBER OF SECTIONS",1X,I3)') NSEC 
WRITE(12,103) 'SECTION', 'AREA', 'INERTIA', 'DEPTH', 'SHEARFACTOR'
ALLOCATE(CSEC(NSEC,4))
DO I=1,NSEC 
	READ(11,*) IDSEC, CSEC(IDSEC,:)
	IDCODE=0 
	DO J=1,3
		IF (CSEC(IDSEC,J)<=0) IDCODE=J
	END DO
	IF (CSEC(IDSEC,4)<0) IDCODE=4
	IF (IDCODE>0) THEN 
		WRITE(12,'(/,1X,"ERROR IN SECTIONS DATA",I2,I1)') IDSEC, IDCODE
		RETURN 
	END IF 
	WRITE(12,104) IDSEC, CSEC(IDSEC,:)
END DO 
!READING WRITING MATERIAL 
READ(11,*) REC
READ(11,*) NMAT
IF (NMAT<1) THEN 
	WRITE(12,'(/,1X,"ERROR NUMB OF MATERIAL",1X)')
	STOP 
	RETURN 
END IF
WRITE(12,'(/,1X,"NUMBER OF MATERIAL",1X,I3)') NMAT
ALLOCATE(CMAT(NMAT,4))
WRITE(12,105) 'IDMATERIAL' ,'YONG', 'POISSON', 'THERMAL' ,'WEIGHT'
DO I=1,NMAT
	READ(11,*) IDMAT, CMAT(IDMAT,:)

	!ERROR CHECKS
	IDCODE=0
	IF (CMAT(IDMAT,1)<=0) IDCODE=1 
	IF ((CMAT(IDMAT,2)<0).OR.(CMAT(IDMAT,2)>0.5)) IDCODE=2
	IF (CMAT(IDMAT,3)<0) IDCODE=3
	IF (CMAT(IDMAT,4)<0) IDCODE=4
	IF (IDCODE>0) THEN 
		WRITE(12,'(/,1X,"ERROR IN MATRICE PROP",I2,I2)')IDCODE, IDMAT
		STOP 
		RETURN 
	END IF
	WRITE(12,106) IDMAT, CMAT(IDMAT,:)
END DO 
	
	
	
	
! READING WRITING CONNECTIVITY 
READ(11,*) REC
READ(11,*) NELE
IF(NELE<1) THEN 
	WRITE(12,'(/,1X,"DATA ERROR")')
	STOP 
	RETURN 
END IF
ALLOCATE(IN(NELE,2))
ALLOCATE(ISEC(NELE))
ALLOCATE(IMAT(NELE))
WRITE(12,'(/,1X,"NUMBER OF ELEMENTS=",1X,I5)') NELE
WRITE(12,107) 'ELEMENT' ,'NODE1' ,'NODE2', 'SECTIONTYPE', 'MATERIALTYPE'
DO I=1,NELE
	READ(11,*) IDELE,(IN(IDELE,J),J=1,2), ISEC(IDELE), IMAT(IDELE)
	WRITE(12,108) IDELE,(IN(IDELE,J),J=1,2), ISEC(IDELE), IMAT(IDELE)

END DO 
RETURN 


!FORMAT LIST 
101 FORMAT(1X,A4,10X,A12,10X,A12)
102 FORMAT(1X,I2,10X,F10.4,10X,F10.4,10X,F10.4)
103 FORMAT(1X,A8,10X,A4,10X,A7,10X,A5,10X,A10)
104 FORMAT(1X,I4,10X,F10.4,10X,F20.4,10X,F10.4,10X,F10.4)
105 FORMAT(1X,A5,10X,A4,10X,A7,10X,A6,10X,A6)
106 FORMAT(1X,I4,10X,F15.4,4X,F10.4,7X,F10.4,5X,F10.4)
107 FORMAT(1X,A7,10X,A5,9X,A5,9X,A11,6X,A12)
108 FORMAT(1X,I4,10X,I4,10X,I4,10X,I4,10X,I4)


END SUBROUTINE GEOMET

SUBROUTINE SCODE
USE GLOBALVAR
INTEGER::I,J,NRES,NLINK,IDNODE,NMAST,NSLAVE
CHARACTER(LEN=80)::REC
ALLOCATE(IDOF(NNODE,3))
!INIITIALISATION OF IDOF
IDOF(:,:)=0
!READING/WRITING RESTRAINS
READ(11,*)REC
READ(11,*)NRES
WRITE(12,'(1X,"NUMBER OF RESTRAINS=",I3)') NRES
WRITE(12,101)'NODE','DIRECTION'
101 FORMAT(1X,A4,T12,A9)
DO I=1,NRES
	READ(11,*) IDNODE,JDIR
	WRITE(12,102) IDNODE,JDIR
	IDOF(IDNODE,JDIR)=-1
END DO 
102 FORMAT(1X,I4,10X,I3)
	!LINKS
READ(11,*)REC
READ(11,*)NLINK
IF(NLINK<0)THEN 
	WRITE(12,'(1X,"NUMBER OF LINKS INOCRRECT")')
	STOP 
	RETURN 
END IF 
WRITE(12,'(/,1X,"NUMBER OF LINKS=",1X,I3)') NLINK
IF (NLINK>0) THEN
	WRITE(12,103)'MASTER', 'SLAVE', 'DIRECTION'
	103 FORMAT(1X,A6,10X,A5,10X,A9)
	DO I=1,NLINK
		READ(11,*) NMAST,NSLAVE,JDIR
		WRITE(12,104) NMAST,NSLAVE,JDIR
		IDOF(NSLAVE,JDIR)=NMAST
	END DO 
END IF
104 FORMAT(1X,I4,10X,I4,10X,I4)
!DOF 
NDOF=0
WRITE(12,'(/,1X,"DOF MATRIX")')
WRITE(12,105)'NODE','UX','UY','ROTZ'
105 FORMAT(1X,A4,10X,A2,10X,A2,10X,A4)
EXT: DO I=1,NNODE
	INT: DO J=1,3
			IF(IDOF(I,J)<0)CYCLE INT !RESTRAINT
			IF(IDOF(I,J)==0) THEN
				NDOF=NDOF+1
				IDOF(I,J)=NDOF
			ELSE 
				IDNODE=IDOF(I,J)
				IDOF(I,J)=IDOF(IDNODE,J)
			END IF 
		END DO INT 
!WRITING DOF MATRIX
		WRITE(12,106) I,IDOF(I,:)
	END DO EXT
RETURN
106 FORMAT(1X,I4,10X,I4,10X,I4,10X,I4)
END SUBROUTINE SCODE

SUBROUTINE LOADS
USE GLOBALVAR
INTEGER::I,NNL,IDNODE,JDIR,JDOF,NEL,IDELE,NEP,NTEND,IDTEND
REAL(4)::PFORCE,ELOAD(5),PRE(4),FP,E1,E2,EM
CHARACTER(LEN=80)::REC
WRITE(12,'(1X,"NODAL AND ELEMENTS LOADS")')
ALLOCATE(VLOADS(NDOF))
ALLOCATE(ELOADS(NELE,5))
ALLOCATE(PRES(NELE,4))
VLOADS(:)=0
ELOADS(:,:)=0
PRES(:,:)=0
!R/W OF NODAL LOADS
READ(11,*)REC
READ(11,*)NNL
IF (NNL<0) THEN 
	WRITE(12,'(/,1X,"ERROR IN NODAL LOAD")')
	STOP 
	RETURN 
END IF 
WRITE(12,'(1X,"NUMBER OF NODAL LOADS=",1X,I3)') NNL
IF (NNL>0) THEN 
WRITE(12,101) 'NODE','DIR','FORCE'
	DO I=1,NNL
		READ(11,*) IDNODE,JDIR,PFORCE
		IF ((IDNODE<1).OR.(IDNODE>NNODE)) THEN
			WRITE(12,'(/,"ERROR IN IDNODE")')
			STOP 
			RETURN 
		ELSE IF((JDIR<1).OR.(JDIR>3)) THEN
			WRITE(12,'(/,"ERROR IN JDIR")')
			STOP 
			RETURN 
		END IF
		WRITE(12,102) IDNODE,JDIR,PFORCE
		JDOF=IDOF(IDNODE,JDIR)
		VLOADS(JDOF)=VLOADS(JDOF)+PFORCE
	END DO
END IF


WRITE(12,'(/,1X,"VECTOR OF NODAL LOADS")')
WRITE(12,103) 'DOF', 'FORCE'
DO I=1,NDOF
	WRITE(12,104) I,VLOADS(I)
END DO

!R/W Elements Loads
READ(11,*)REC
READ(11,*)NEL
IF (NEL<0) THEN 
	WRITE(12,'(/,1X,"(ERROR IN NUMBER OF ELEMNT LOADS)")')
	STOP 
	RETURN 
END IF
WRITE(12,'(/,1X,"NUMBER OF ELEMENT LOADS=",1X,I3)') NEL
IF (NEL>0) THEN 
	WRITE(12,105) 'ELEMENT', 'PX', 'PY1', 'PY2', 'DTOP', 'DBOT'
	DO I=1,NEL
		READ(11,*) IDELE, ELOAD(1:5)
		IF ((IDELE<1).OR.(IDELE>NELE)) WRITE(12,'("ERROR IN IDELEMENT")')
		WRITE(12,106) IDELE, ELOAD(1:5)
		ELOADS(IDELE,1:5)=ELOADS(IDELE,1:5)+ELOAD(1:5)
	END DO
END IF 
!R/W Prestressing 
READ(11,*)REC
READ(11,*)NEP
IF(NEP<0) THEN 
	WRITE(12,'(/,"ERROR IN NUMBER OF PRESTRASING LOAD")')
END IF 
WRITE(12,'(/,1X,"NUMBER OF PRESSTRAISING ELEM=",1X,I3)') NEP
IF (NEP>0) THEN 
	DO I=1,NEP
		READ(11,*)IDELEM,NTEND
		IF ((IDELE<1).OR.(IDELE>NELE)) THEN 
			WRITE(12,'(/,"ERROR IN TENDON ELEMENT ASSOCIATION")')
		END IF 
		FP=0
		E1=0
		E2=0
		EM=0
		WRITE(12,'(1X,"PRESTRESED ELEM=",I3)') IDELE
		WRITE(12,107) 'IDTENDON','P','E1','EM','E2'
		DO J=1,NTEND 
			READ(11,*) IDTEND,PRE(1:4)
			WRITE(12,108) IDTEND,PRE(1:4) 
			FP=FP+PRE(1)
			E1=E1+PRE(1)*PRE(2)
			EM=EM+PRE(1)*PRE(3)
			E2=E2+PRE(1)*PRE(4)
		END DO 
		PRES(IDELE,1)=FP
		PRES(IDELE,2)=E1/FP
		PRES(IDELE,3)=EM/FP
		PRES(IDELE,4)=E2/FP
		WRITE(12,109) 'EQUIVALENT PRES FORCES',FP,E1/FP,EM/FP,E2/FP
	END DO 
END IF 
RETURN 
		 
101 FORMAT(1X,A4,10X,A3,10X,A4)
102 FORMAT(1X,I4,10X,I4,10X,F10.4)
103 FORMAT(1X,A3,10X,A5)
104 FORMAT(1X,I2,10X,F10.4)
105 FORMAT(1X,A6,16X,A2,18X,A2,20X,A2,10X,A4,15X,A4)
106 FORMAT(1X,I2,10X,F10.4,10X,F10.4,10X,F10.4,10X,F10.4,10X,F10.4)
107 FORMAT(1X,A6,10X,A1,10X,A2,10X,A2,10X,A2)
108 FORMAT(1X,I3,10X,F10.4,10X,F10.4,10X,F10.4,10X,F10.4)
109 FORMAT(1X,A23,10X,F10.4,10X,F10.4,10X,F10.4,10X,F10.4)


END SUBROUTINE LOADS

SUBROUTINE ASSEMB
USE GLOBALVAR
INTEGER::I,J,NCODE(6)
ALLOCATE(VK(NDOF,NDOF))
VK(:,:)=0.
DO NE=1,NELE
	CALL MKK(NE)
	N1=IN(NE,1)
	N2=IN(NE,2)
	NCODE(1:3)=IDOF(N1,1:3)
	NCODE(4:6)=IDOF(N2,1:3)
	EXT:DO I=1,6
		IC=NCODE(I)
		IF(IC<0) CYCLE EXT 
		VLOADS(IC)=VLOADS(IC)+EQFG(I)
		INT:DO J=1,6
		JC=NCODE(J)
		IF (JC<0) CYCLE INT
		VK(IC,JC)=VK(IC,JC)+ST(I,J)
		END DO INT 
	END DO EXT 
END DO 
RETURN
END SUBROUTINE ASSEMB


SUBROUTINE MKK(NE) 
USE GLOBALVAR 
INTEGER::I,J,N1,N2,ISC,IMT
REAL(4)::DX,DY,AL,CA,SA,AA,AJ,AH,CHI,EMOD,POISSON,ALPHA,WEIGHT,GMOD,PHI,&
PX,PY1,PY2,DTBOT,DTTOP,FP,E1,EM,E2,TETA1,TETA2,CURV,WW,TMP(6,6),T(6,6),&
EQF(6)




!GEOMETRIC PROPETIES OF THE ELEMENT 

N1=IN(NE,1)
N2=IN(NE,2)
DX=COORD(N2,1)-COORD(N1,1)
DY=COORD(N2,2)-COORD(N1,2)
AL=SQRT(DX**2+DY**2) 
CA=DX/AL
SA=DY/AL



	! SECTION AND MATERIAL
	ISC = ISEC(NE)
	IMT = IMAT(NE)
	AA  = CSEC(ISC,1)
	AJ  = CSEC(ISC,2)
	AH  = CSEC(ISC,3)
	CHI = CSEC(ISC,4)
	EMOD = CMAT(IMT,1)
	POISSON = CMAT(IMT,2)
	ALPHA = CMAT(IMT,3)
	WEIGHT = CMAT(IMT,4)
	GMOD = EMOD/(2.*(1.+POISSON))
	PHI = 12.*CHI*EMOD*AJ/(AL**2.*GMOD*AA)

	


	! LOCAL STIFFNESS MATRIX
	ST(:,:) = 0.
	ST(1,1) = EMOD*(AA/AL)
	ST(2,2) = 12.*EMOD*AJ/((1.+PHI)*AL**3.)
	ST(3,2) = 6.*EMOD*AJ/((1.+PHI)*AL**2.)
	ST(3,3) = (4.+PHI)*EMOD*AJ/((1.+PHI)*AL)
	ST(4,1) = -ST(1,1)
	ST(4,4) = ST(1,1)
	ST(5,2) = -ST(2,2)
	ST(5,3) = -ST(3,2)
	ST(5,5) = ST(2,2)
	ST(6,2) = ST(3,2)
	ST(6,3) = (2.-PHI)*EMOD*AJ/((1.+PHI)*AL)
	ST(6,5) = -ST(3,2)
	ST(6,6) = ST(3,3)


	! COPY THE ELEMENT IN THE UPPER PART (SYMMETRIC)
	DO  I = 1,6
		DO  J = I+1,6
			ST(I,J) = ST(J,I)
		END DO
	END DO
	
!SAVE INFORMATION IN A TEMPORARY FILE 
WRITE(9,*) ST(:,:)

	! TRANSFORMATION MATRIX
	T(:,:) = 0.
	T(1,1) = CA
	T(1,2) = -SA
	T(2,1) = SA
	T(2,2) = CA
	T(3,3) = 1.
	T(4,4) = CA
	T(4,5) = -SA
	T(5,4) = SA
	T(5,5) = CA
	T(6,6) = 1.


!SAVE INFORMATION 
WRITE(9,*) T(:,:)


!!STIFNESS MATRIC IN THE GLOBAL REF
TMP=MATMUL(T,ST)
ST=MATMUL(TMP,TRANSPOSE(T))


! INITIALIZATION OF EQUIVALENT NODAL FORCE VECTOR
	EQF(:) = 0.
	! DISTRIBUTED LOADS
	PX  = ELOADS(NE,1)
	PY1 = ELOADS(NE,2)
	PY2 = ELOADS(NE,3)
	DTTOP  = ELOADS(NE,4)
	DTBOT = ELOADS(NE,5)
	! SELF-WEIGHT
	PG = WEIGHT*AA
	PX = PX-PG*SA
	PY1 = PY1-PG*CA
	PY2 = PY2-PG*CA
	QQ = PY2-PY1
	! INFORMATION ON PRESTRESSING
	FP = PRES(NE,1)
	E1 = PRES(NE,2)
	EM = PRES(NE,3)
	E2 = PRES(NE,4)
	TETA1 = -1./AL*(3.*E1+E2-4.*EM) 
	TETA2 = 1./AL*(E1+3.*E2-4.*EM)
	CURV  = 4.*(E1+E2-2.*EM)/(AL**2.)
	WW = FP*CURV
	PY1 = PY1+WW
	PY2 = PY2+WW
	! COMPUTATION OF LONGITUDINAL LOADS
	EQF(1) = EQF(1)+PX*AL/2.
	EQF(4) = EQF(4)+PX*AL/2.
	! COMPUTATION OF TRANSVERSAL LOAD (CONSTANT PART)
	EQF(2) = EQF(2)+PY1*AL/2.
	EQF(3) = EQF(3)+PY1*AL**2./12.
	EQF(5) = EQF(5)+PY1*AL/2.
	EQF(6) = EQF(6)-PY1*AL**2./12.
	! COMPUTATION OF TRANSVERSAL LOAD (LINEAR PART)
	COEF_A = AL/(12.*EMOD*AJ)
	COEF_B = CHI/(AL*AA*GMOD)
	COEF_C = COEF_B/(COEF_A+COEF_B)
	EQF(2) = EQF(2)+9./60.*QQ*AL*(1.+COEF_C/9.)
	EQF(3) = EQF(3)+1./30.*QQ*AL**2.*(1.+COEF_C/4.)
	EQF(5) = EQF(5)+21./60.*QQ*AL*(1-COEF_C/21.)
	EQF(6) = EQF(6)-1./20.*QQ*AL**2.*(1-COEF_C/6.)
	! THERMAL LOADS (ELONGATION)
	DELTA_TM = (DTTOP+DTBOT)/2.
	FH = EMOD*AA*ALPHA*DELTA_TM ! FORCE
	EQF(1) = EQF(1)-FH
	EQF(4) = EQF(4)+FH

	! THERMAL LOADS (FLEXURAL BEHAVIOUR)
	DELTA_TD = (DTTOP-DTBOT)/2.
	FM = 2.*ALPHA*DELTA_TD*EMOD*AJ/AH
	EQF(3) = EQF(3)+FM
	EQF(6) = EQF(6)-FM
	!SAVE INFORMATION TEMPORARY FILE 
WRITE(9,*) EQF(:)	

	! PRESTRESSING LOADS
	EQF(1) = EQF(1)+FP
	EQF(2) = EQF(2)+FP*TETA1
	EQF(3) = EQF(3)-FP*E1
	EQF(4) = EQF(4)-FP
	EQF(5) = EQF(5)-FP*TETA2
	EQF(6) = EQF(6)+FP*E2



!TRANSFORMATION LOCAL -> GLOBAL
EQFG=MATMUL(T,EQF)


RETURN 

END SUBROUTINE MKK

 SUBROUTINE SOLVE 
USE GLOBALVAR
INTEGER::I,J,K,JDOF
REAL(4)::DNOD(3),DMX(3),DMN(3),PVAL
ALLOCATE(VDISP(NDOF))
! Initialization of the vector of the constant terms
! Load vector - > Displacement vector
VDISP = VLOADS
! Gaussian elimination method
DO I=1,NDOF
	DO K=I+1,NDOF
        VK(I,K) = VK(I,K)/VK(I,I)
   	END DO
	VDISP(I) = VDISP(I)/VK(I,I) 
	VK(I,I) = 1.
	DO J=I+1,NDOF
		DO K = I+1,NDOF
			VK(J,K) = VK(J,K)-VK(J,I)*VK(I,K)
		END DO
        VDISP(J) = VDISP(J)-VK(J,I)*VDISP(I)
		VK(J,I) = 0.
	END DO
END DO
! Backward substitution
DO I=NDOF,1,-1
	DO K=I+1,NDOF
        VDISP(I) = VDISP(I)-VK(I,K)*VDISP(K)
	END DO 
END DO

!WRITING THE NODAL DISPLACEMENTS 
WRITE(12,'(/,"SUMMARY OF THE RESULTS")')
WRITE(12,'(/,"NODAL DISPLACEMENTS")')
WRITE(12,102) 'NODE','UX','UY','ROTZ'
DNOD=0.
DMX=-1.E6
DMN=1.E6
DO I=1,NNODE
	DO J=1,3
		IF(IDOF(I,J)<=0) THEN 
			DNOD(J)=0.
		ELSE 
			JDOF=IDOF(I,J)
			DNOD(J)=VDISP(JDOF)
		END IF 
		PVAL=DMX(J)
		DMX(J)=MAX(PVAL,DNOD(J))
		PVAL=DMN(J)
		DMN(J)=MIN(PVAL,DNOD(J))
	END DO 
	WRITE(12,103) I, (DNOD(J), J=1,3)
END DO 
WRITE(12,104) 'MAX', (DMX(J),J=1,3)
WRITE(12,104) 'MIN', (DMN(J),J=1,3)
WRITE(12,'(/,"DISPLACEMENT AT DEGRESS OF FREEDOM")')
WRITE(12,105) 'DOF', 'DISPLACEMENT' 

DO I=1,NDOF
	WRITE(12,106) I,VDISP(I)
END DO 
RETURN 

!FORMAT LIST 
102 FORMAT(A4,22X,A2,23X,A2,23X,A4)
103 FORMAT(I3,10X,F15.4,10X,F15.4,10X,F15.4)
104 FORMAT(A3,10X,F15.4,10X,F15.4,10X,F15.4)
105 FORMAT(A3,10X,A13)
106 FORMAT(I3,10X,F15.4)

END SUBROUTINE SOLVE 

SUBROUTINE STRESS 
USE GLOBALVAR
INTEGER::I,J,JDOF,NCODE(6)
REAL(4)::QS(6),T(6,6),EQF(6),QF(6)
REWIND(9)
WRITE(12,'(/,1X,"FORCES AT END NODES")')
WRITE(12,100) 'ELEM', 'N1', 'T1', 'M1', 'N2', 'T2', 'M2'
!LOOP FOR ALL THE ELEMENTS 
DO NE=1,NELE
	N1=IN(NE,1)
	N2=IN(NE,2)
	NCODE(1:3)=IDOF(N1,1:3)
	NCODE(4:6)=IDOF(N2,1:3)
	!READING FROM TEMPORARY FILE
	READ(9,*) ST
	READ(9,*) T 
	READ(9,*) EQF  
	!NODAL DISPLACEMENT 
	DO I=1,6
		JDOF=NCODE(I) 
		IF(JDOF==-1) THEN 
			QS(I)=0.
		ELSE 
			QS(I)=VDISP(JDOF)
		END IF
	END DO 
	!TRANSFORMATION 
	
	QSL=MATMUL(TRANSPOSE(T),QS)

	!WRITE THE END FORCES 
	DO I=1,6
		QF(I)=-EQF(I) 
		DO J=1,6
			QF(I)=QF(I)+ST(I,J)*QSL(J)
		END DO 
	END DO 
	WRITE(12,101) NE, (QF(J), J=1,6)
END DO 
RETURN 
!FORMAT LIST
100 FORMAT(A4,31X,A2,33X,A2,33X,A2,33X,A2,33X,A2,33X,A2)
101 FORMAT(I2,10X,F25.4,10X,F25.4,10X,F25.4,10X,F25.4,10X,F25.4,10X,F25.4)
END SUBROUTINE STRESS

SUBROUTINE JOINTS 
USE GLOBALVAR
INTEGER::I,K,JDIR
INTEGER::NELR,NELL,NTDIS,NRDIS
INTEGER::IDNODE,N1,N2
REAL(4)::STIFF,ADIS,VKJJ
CHARACTER(LEN=80)::REC
WRITE(12,'(1X,"ELASTIC RESTRAINS/ELASTIC LINKS-DISPLACMENTS")')
!ELASTIC RESTRAINS 
READ(11,*)REC
READ(11,*)NELR
IF(NELR<0) THEN 
	WRITE(12,'(1X,"NUMBER OF ELASTIC RES UNCORECT")')
	STOP 
	RETURN 
END IF
WRITE(12,'(1X,"NUMBER OF ELASTIC RESTRAIAINTS=",1X,I3)') NELR
IF(NELR>0) THEN 
	WRITE(12,101) 'NODE', 'DIRECTION', 'STIFFNESS'
	DO I=1,NELR
		READ(11,*) IDNODE, JDIR, STIFF
		WRITE(12,102) IDNODE, JDIR, STIFF
		JDOF=IDOF(IDNODE,JDIR)
		VK(JDOF,JDOF)=VK(JDOF,JDOF)+STIFF
	END DO 
END IF 
!ELASTIC LINKS
READ(11,*) REC 
READ(11,*) NELL 
IF(NELL<0) THEN 
	WRITE(12,'(1X,"NUMBER OF ELASTIC LINKS UNCOREXT")')
	STOP 
	RETURN 
END IF 
WRITE(12,'(/,1X,"NUMBER OF ELASTIC LINKS=",1X,I3)') NELL 
IF(NELL>0) THEN 
	WRITE(12,103) 'NODE1','NODE2','DIRECTION','STIFNESS'
	DO I=1,NELL 
			READ(11,*) N1,N2,JDIR,STIFF
			WRITE(12,104) N1,N2,JDIR,STIFF
			JDOF1=IDOF(N1,JDIR)
			JDOF2=IDOF(N2,JDIR) 
			VK(JDOF1,JDOF1)=VK(JDOF1,JDOF1)+STIFF
			VK(JDOF2,JDOF2)=VK(JDOF2,JDOF2)+STIFF
			VK(JDOF1,JDOF2)=VK(JDOF1,JDOF2)-STIFF
			VK(JDOF2,JDOF1)=VK(JDOF1,JDOF2)
	END DO
END IF 
!IMPOSED ABSOLUTE DISPLACEMENTS 
READ(11,*) REC  
READ(11,*) NTDIS 
IF(NTDIS<0) THEN 
	WRITE(12,'(1X,"NUMBER OF IMPOSED DISPLACEMENT UNCORECT")')
	STOP 
	RETURN 
END IF 
WRITE(12,'(/,1X,"NUMBER OF IMPOSED ABSOLUTE DISP=",1X,I3)') NTDIS
IF(NTDIS>0) THEN 
	WRITE(12,105) 'NODE', 'DIRECTION', 'DISPLACEMENT'
	DO I=1,NTDIS 
		READ(11,*) IDNODE, JDIR, ADIS 
		WRITE(12,106)IDNODE, JDIR, ADIS 
		JDOF=IDOF(IDNODE,JDIR) 
		VKJJ=VK(JDOF,JDOF) 
		VK(JDOF,1:NDOF)=0.
		VLOADS(1:NDOF)=VLOADS(1:NDOF)-VK(1:NDOF,JDOF)*ADIS
		VK(1:NDOF,JDOF)=0.
		VK(JDOF,JDOF)=VKJJ
		VLOADS(JDOF)=VKJJ*ADIS
	END DO 
END IF 
!IMPOSED RELATIVE DISP 
READ(11,*) REC 
READ(11,*)NRDIS
IF(NRDIS<0) THEN 
	WRITE(12,'(1X,"ERROR IN NUMBER OF RELATIVE DISPLACEMENT")')
	STOP 
	RETURN 
END IF 
WRITE(12,'(/,1X,"NUMBER OF RELATIVE DISPL=",1X,I3)') NRDIS
IF(NRDIS>0) THEN 
	WRITE(12,107) 'NODE1', 'NODE2', 'DIRECTION', 'DISPLACEMENT'
	DO I=1,NRDIS
		READ(11,*) N1,N2,JDIR,ADIS
		WRITE(12,108) N1,N2,JDIR,ADIS
		JDOF1=IDOF(N1,JDIR) 
		JDOF2=IDOF(N2,JDIR)
		IF((JDOF1==-1).OR.(JDOF2==-1)) THEN 
			WRITE(12,'(/,1X,"ERROR IN RELATIVE DSIPLACMENT")')
			STOP 
			RETURN 
		END IF 
		VKJJ=VK(JDOF1,JDOF1)+VK(JDOF2,JDOF2) 
		DO K=1,NDOF 
			VK(JDOF1,K)=VK(JDOF1,K)+VK(JDOF2,K) 
		END DO 
		VLOADS(JDOF1)=VLOADS(JDOF1)+VLOADS(JDOF2)
		DO K=1,NDOF
			VK(JDOF2,K)=0.
			VK(K,JDOF1)=VK(K,JDOF1)+VK(K,JDOF2) 
			VLOADS(K)=VLOADS(K)-VK(K,JDOF2)*ADIS
			VK(K,JDOF2)=0.
		END DO 
		VK(JDOF1,JDOF1)=VK(JDOF1,JDOF1)+VKJJ 
		VK(JDOF1,JDOF2)=-VKJJ
		VK(JDOF2,JDOF1)=-VKJJ
		VK(JDOF2,JDOF2)=VKJJ 
		VLOADS(JDOF1)=VLOADS(JDOF1)-VKJJ*ADIS
		VLOADS(JDOF2)=VKJJ*ADIS
	END DO 
END IF 
!INCLINED CONSTRAINS 
RETURN !FORMAT LIST 
101 FORMAT(A4,10X,A9,10X,A9) 
102 FORMAT(I2,10X,I2,10X,F10.4)
103 FORMAT(A5,10X,A5,10X,A9,10X,A9)
104 FORMAT(I2,10X,I2,10X,I2,10X,F10.4) 
105 FORMAT(A4,10X,A9,10X,A12) 
106 FORMAT(I2,10X,I2,10X,F10.4)
107 FORMAT(A5,10X,A5,10X,A9,10X,A12)
108 FORMAT(I2,10X,I2,10X,I2,F10.4)

END SUBROUTINE JOINTS


			





